configurations.all {
  resolutionStrategy {
    eachDependency { details ->
      if (details.requested.group == 'org.gradle') return

      switch(details.requested.group) {
        case 'org.apache.maven': details.useVersion('3.x'); return;
        default: break;
      }
      
      // Gradle refuses to override the artifact names, no idea why
      switch(details.requested.name) {
        // Jetty
        case 'servlet-api': details.useTarget('javax.servlet:servlet-api-3.1:debian'); return;
        case 'jetty-util': details.useTarget('org.eclipse.jetty:jetty9-util:debian'); return;
        case 'jetty': details.useTarget('org.eclipse.jetty:jetty9-server:debian'); return;

        // Eclipse Aether
        case 'aether-api': details.useTarget('org.eclipse.aether:maven-resolver-api:debian'); return;
        case 'aether-impl': details.useTarget('org.eclipse.aether:maven-resolver-impl:debian'); return;
        case 'aether-spi': details.useTarget('org.eclipse.aether:maven-resolver-spi:debian'); return;
        case 'aether-util': details.useTarget('org.eclipse.aether:maven-resolver-util:debian'); return;
        case 'aether-connector-wagon': details.useTarget('org.eclipse.aether:maven-resolver-connector-basic:debian'); return;

        // Misc
        case 'antlr': details.useVersion('2.x'); return;
        case 'commons-collections': details.useVersion('3.x'); return;
        case 'bcpg-jdk15on': details.useTarget('org.bouncycastle:bcpg:debian'); return;
        case 'bcprov-jdk15on': details.useTarget('org.bouncycastle:bcprov:debian'); return;
        case 'bsh': details.useTarget('org.beanshell:bsh:debian'); return;
        case 'dom4j': details.useTarget('org.dom4j:dom4j:debian'); return;
        case 'guava-jdk5': details.useTarget('com.google.guava:guava:debian'); return;
        case 'jcifs': details.useTarget('jcifs:jcifs:debian'); return;
        case 'junit': details.useVersion('4.x'); return;
        case 'wagon-http-shared4': details.useTarget('org.apache.maven.wagon:wagon-http-shared4:debian'); return;
        case 'jansi': details.useVersion('1.x'); return;
        default:
          if (!details.requested.version.endsWith('.x')) {
            details.useVersion('debian');
          }
          break;
      }
    }
  }
}
